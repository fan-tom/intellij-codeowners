// https://docs.github.com/en/github/creating-cloning-and-archiving-repositories/about-code-owners#codeowners-syntax
{
  parserClass               = "com.github.fantom.codeowners.lang.kind.github.parser.CodeownersParser"
  extends                   = "com.github.fantom.codeowners.lang.CodeownersElementImpl"

  psiClassPrefix            = "Codeowners"
  psiImplClassSuffix        = "Impl"
  psiPackage                = "com.github.fantom.codeowners.lang.kind.github.psi"
  psiImplPackage            = "com.github.fantom.codeowners.lang.kind.github.psi.impl"

  elementTypeHolderClass    = "com.github.fantom.codeowners.lang.kind.github.psi.CodeownersTypes"
  elementTypeClass          = "com.github.fantom.codeowners.lang.kind.github.psi.CodeownersElementType"
//  elementTypeClass          = "com.github.fantom.codeowners.lang.CodeownersElementType"
  tokenTypeClass            = "com.github.fantom.codeowners.lang.kind.github.psi.CodeownersTokenType"
//  tokenTypeClass            = "com.github.fantom.codeowners.lang.CodeownersTokenType"

  tokens = [
    CRLF                    = "regexp:[\s\r\n]+"
    HEADER                  = "regexp:###.*"
    SECTION                 = "regexp:##.*"
    COMMENT                 = "regexp:#.*"
    SLASH                   = "/"
    AT                      = "@"
//    TEAMNAME                = "regexp:[\w-]+"
//    USERNAME                = "regexp:[\w\d-]+"
//    DOMAIN                  = "regexp:\w+(\.\w+)+"
    VALUE                   = "regexp:[^@/\s]+"
//    SPACES                  = 'regexp:\s+'
  ]

  name("Entry.*")           = "entry"
  mixin("Entry")            = "com.github.fantom.codeowners.lang.kind.github.psi.impl.CodeownersEntryExtImpl"
  mixin("NamedOwner")       = "com.github.fantom.codeowners.lang.kind.github.psi.impl.CodeownersNamedOwnerExtImpl"
  implements("Entry")       = "com.github.fantom.codeowners.lang.CodeownersEntryBase"
}

codeownersFile          ::= item_ *
private item_           ::= HEADER | SECTION | COMMENT | Pattern | CRLF

Pattern                 ::= (EntryDirectory | EntryFile) /*SPACES*/ CRLF Owners /*CRLF*/

Entry                   ::= '/' ? <<list_macro VALUE>>
EntryDirectory          ::= '/' ? <<list_macro VALUE>> '/' { extends = "EntryFile"}
EntryFile               ::= '/' ? <<list_macro VALUE>>     { extends = "Entry"}

Owners                  ::= Owner (CRLF Owner)*
Owner                   ::= Email | NamedOwner
NamedOwner              ::= '@' OwnerName

OwnerName               ::= Team | UserName
Team                    ::= OrgName '/' TeamName

Email                   ::= UserName '@' Domain

UserName                ::= VALUE
OrgName                 ::= VALUE
TeamName                ::= VALUE
Domain                  ::= VALUE

private meta list_macro ::= <<p>> + ('/' <<p>> +) *
